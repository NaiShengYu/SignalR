plugins {
    id 'java'
    id 'maven'
}

group 'com.microsoft.aspnet'
version '0.1.0-preview1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation "org.java-websocket:Java-WebSocket:1.3.8"
    implementation 'com.google.code.gson:gson:2.8.5'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

task generatePOM {
    pom {
        project {
            inceptionYear '2018'
            description 'ASP.NET Core SignalR Client for Java applications'
            url 'https://github.com/aspnet/SignalR'
            name groupId + ':' + artifactId
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git://github.com/aspnet/SignalR.git'
                developerConnection 'scm:git:git://github.com/aspnet/SignalR.git'
                url 'http://github.com/aspnet/SignalR/tree/master'
            }
            developers {
                developer {
                    id 'microsoft'
                    name 'Microsoft'
                }
            }
        }
    }.writeTo("signalr-0.1.0-preview1.pom")

    ant.move file: "signalr-0.1.0-preview1.pom",
            todir: "${buildDir}/libs"
}
